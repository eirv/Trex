plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'io.github.eirv.trex'
    compileSdk rootProject.ext.compileSdk
    buildToolsVersion rootProject.ext.buildTools
    ndkVersion rootProject.ext.ndk

    defaultConfig {
        minSdk rootProject.ext.minSdk
        targetSdk rootProject.ext.targetSdk
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.version
        consumerProguardFile 'consumer-rules.pro'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        externalNativeBuild.cmake.arguments
                << '-DANDROID_STL=none'
                << '-DCMAKE_BUILD_TYPE=Release'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java.srcDirs += '../common'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.java
        targetCompatibility rootProject.ext.java
    }

    buildFeatures {
        prefab true
        buildConfig false
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
            version rootProject.ext.cmake
        }
    }
}

dependencies {
    compileOnly project(':stub')
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'dev.rikka.ndk.thirdparty:cxx:1.2.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

preBuild.dependsOn ':stub:assembleDebug'

afterEvaluate {
    tasks.withType(JavaCompile) {
        def stubBuildPath = project(':stub').buildDir.path
        def stubJar = "${stubBuildPath}/intermediates/aar_main_jar/debug/classes.jar"

        def androidHome = android.sdkDirectory.path
        def compileSdk = android.compileSdk
        def androidJar = "${androidHome}/platforms/android-${compileSdk}/android.jar"

        options.compilerArgs
                << '-bootclasspath'
                << "${stubJar}:${androidJar}"
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = rootProject.ext.groupId
            artifactId = 'android'
            version = rootProject.ext.version
            artifact("$buildDir/outputs/aar/android-release.aar")

            pom {
                name = rootProject.ext.name
                packaging = 'aar'
                description = rootProject.ext.description
            }
        }
    }

    repositories {
        maven {
            name = "LocalMaven"
            url = uri(rootProject.ext.mavenUrl)
        }
    }
}
